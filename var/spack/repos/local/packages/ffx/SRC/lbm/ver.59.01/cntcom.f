      SUBROUTINE CNTCOM(IPART,NG,NP,NC,MBC,NBC,LBC,
     *                  MPB,NDOM,LDOM,NBPSND,NBPRCV,IUT6)
      IMPLICIT NONE
      INTEGER*4 IPART,NG,NP,NC,MBC,NBC(NC),LBC(5,MBC,NC),MPB,NDOM,
     *          LDOM(NDOM),IUT6 
      INTEGER*4 NBPSND(NDOM),NBPRCV(NDOM)
C
      INTEGER*4 IBC,IC,ID,JPART,IDOM,IERR
C
C     ====FUNCTION====
C     COUNT NUMBER OF B.C. DATA
C
C     ====VARIABLES LIST====
C[IN]
C     IPART          :DOMAIN NUMBER THAT THIS TASK SHOULD COMPUTE/IS COMPUTING. 
C                     IPART BEING SET ZERO MEANS THAT THE PROGRAM SHOULD RUN/IS 
C                     RUNNING IN SERIAL MODE.
C     NG             :CUBE SIZE (=2^N)
C     NP             :NUMBER OF PARTICLES (=15)
C     NC             :NUMBER OF CUBES IN SUB-DOMAIN
C     MBC            :MAX. NUMBER OF B.C GROUPS IN CUBES (=152)
C     NBC(IC)        :NUMBER OF B.C. GROUPS IN CUBES
C     LLEVEL(IC)     :LEVEL OF CUBES, WHICH INDICATE THE GRID RESOLUTION. 
C                     LEVEL=1 CORRESPONTDS THE FINEST GRID SIZE. A GRID SIZE
C                     WILL BE TWICE WITH ONE INCREMENT OF THE LEVEL.
C     LBC(II,IBC,IC) :ATTRIBUTE DATA OF B.C. GROUPS
C                      II=1 B.C. GROUP ID (1-26) IN AN ADJACENT CUBE
C                      II=2 SUB-DOMAIN NUMBER OF AN ADJACENT CUBE
C                      II=3 CUBE NUMBER OF AN ADJACENT CUBE IN A DOMAIN
C                      II=4 RELATIVE LEVEL OF AN ADJACENT CUBE
C                       (-1: FINE, 0:SAME, 1:COARSE)
C                      II=5 POSITION IN COARSER CUBE
C     NDOM           :NUMBER OF THE NEIGHBORING SUB-DOMAINS
C     LDOM(IDOM)     :NEIGHBORING SUB-DOMAIN NUMBER
C 
C[OUT]
C    MPB             :MAX. OF NPBRCV(IDOM) AND NBPSND(IDOM)  
C    NBPRCV(IDOM)    :NUMBER OF DATA TO BE RECEIVED FROM THE NEIGHBORING SUB-DOMAINS
C    NBPSND(IDOM)    :NUMBER OF DATA TO BE RECEIVED FROM THE NEIGHBORING SUB-DOMAINS
C

      IERR=0
C
      IF(IPART.EQ.0) RETURN
C
      DO 100 IDOM=1,NDOM
          NBPSND(IDOM)=1
  100 CONTINUE     
C
C
      DO 1000 IC=1,NC
      DO 1100 IBC=1,NBC(IC)
          ID    =LBC(1,IBC,IC)
          JPART =LBC(2,IBC,IC)
C
          IF(IPART.EQ.JPART)  GOTO 1100
C
CC[OTHER DOMAIN]
C
           DO 1200 IDOM=1,NDOM
               IF(LDOM(IDOM).EQ.JPART) GOTO 1300
 1200      CONTINUE
 1300      CONTINUE
C 
              NBPSND(IDOM)=NBPSND(IDOM)+4
C
              IF(ID.LE.6) THEN
C             FACE-CONNECTION
                  NBPSND(IDOM)=NBPSND(IDOM)+NP*(NG+1)*(NG+1)
              ELSE IF (ID.GE.7 .AND. ID.LE.18) THEN
C             EDGE-CONNECTION
                  NBPSND(IDOM)=NBPSND(IDOM)+NP*(NG+1)
              ELSE
C             POINT-CONNECTION
                  NBPSND(IDOM)=NBPSND(IDOM)+NP
              ENDIF
C
 1100      CONTINUE
 1000  CONTINUE
C
          MPB=0
          DO 2000 IDOM=1,NDOM
              IF(MPB.LT.NBPSND(IDOM)) MPB=NBPSND(IDOM)
 2000     CONTINUE   
C
          CALL DDCOM1(NDOM,LDOM,NBPSND,NBPRCV,IERR)
C
          DO 2100 IDOM=1,NDOM
              IF(MPB.LT.NBPRCV(IDOM)) MPB=NBPRCV(IDOM)
 2100     CONTINUE   
C
      RETURN
      END
