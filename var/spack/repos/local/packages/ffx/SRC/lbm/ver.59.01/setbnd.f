      SUBROUTINE SETBND(ITIME,IPART,NP,LVEL,NG,NC,MBC,MBC1,MBC2,MBC3,
     *                  NBC,LLEVEL,LBC,MPBOUN,NPBOUN,LPBOUN,F,FEQ,
     *                  VISCM,DSCALE,VSCALE,CVEL,WF,
     *                  MDOM,MPB,NDOM,LDOM,NBPSND,NBPRCV,
     *                  BUFSND,BUFRCV,
     *                  FBC1,FBC1W,FBC2,FBC2W,FBC3,FBC3W,
     *                  IUT6,IUT0,IERR)
      IMPLICIT NONE
C
      INTEGER*4 ITIME,IPART,NP,LVEL(3,NP),
     *          NG,NC,MBC,MBC1,MBC2,MBC3,MPB,MDOM,NDOM
      INTEGER*4 NBC(NC),LLEVEL(NC),LBC(5,MBC,NC)
      INTEGER*4 MPBOUN,NPBOUN(NC),LPBOUN(5,MPBOUN,NC)
      INTEGER*4 LDOM(NDOM),NBPSND(NDOM),NBPRCV(NDOM)
      REAL*8    F  (0:NG+2,0:NG+2,0:NG+2,NP,NC)
      REAL*8    FEQ(0:NG+2,0:NG+2,0:NG+2,NP)
      REAL*8    VISCM,DSCALE,VSCALE,CVEL(3,NP),WF(NP)
      REAL*8    FBC1 (NP,NG+1,NG+1,MBC1,NC),
     *          FBC1W(NP,NG+1,NG+1,MBC1,NC),
     *          FBC2 (NP,NG+1,     MBC2,NC),
     *          FBC2W(NP,NG+1,     MBC2,NC),
     *          FBC3 (NP,          MBC3,NC),
     *          FBC3W(NP,          MBC3,NC)
      REAL*8    BUFSND(MPB,*),BUFRCV(MPB,*)
      INTEGER*4 IUT6,IUT0,IERR
C
      REAL*8    RHO,U0,V0,W0,UU,CU,FBUF,TAU0,TAU3(3),VIS1,VIS2,VIS3 
      INTEGER*4 IC,ICB,NBC1,NBC2,NBC3,II,I,J,K,IP,ID,
     *          MPBDOM,IDOM,ILVL1,ILVL2,ILVL3
C
C     ====FUNCTION====
C     MAIN ROUTINE FOR SETTING BOUNDARY CONDITIONS
C
C     ====VARIABLES LIST====
C[IN]
C     ITIME          :TIME STEP
C     IPART          :DOMAIN NUMBER THAT THIS TASK SHOULD COMPUTE/IS COMPUTING. 
C                     IPART BEING SET ZERO MEANS THAT THE PROGRAM SHOULD RUN/IS 
C                     RUNNING IN SERIAL MODE.
C     NG             :CUBE SIZE (=2^N)
C     NC             :NUMBER OF CUBES IN SUB-DOMAIN
C     MBC            :MAX. NUMBER OF B.C GROUPS IN CUBES (=152)
C     MBC1           :MAX. NUMBER OF FACE-TYPE  B.C. GROUPS (=24)
C     MBC2           :MAX. NUMBER OF EDGE-TYPE  B.C. GROUPS (=24)
C     MBC3           :MAX. NUMBER OF POINT-TYPE B.C. GROUPS (= 8)
C     NBC(IC)        :NUMBER OF B.C. GROUPS IN CUBES
C     LLEVEL(IC)     :LEVEL OF CUBES, WHICH INDICATE THE GRID RESOLUTION. 
C                     LEVEL=1 CORRESPONTDS THE FINEST GRID SIZE. A GRID SIZE
C                     WILL BE TWICE WITH ONE INCREMENT OF THE LEVEL.
C     LBC(II,IBC,IC) :ATTRIBUTE DATA OF B.C. GROUPS
C                      II=1 B.C. GROUP ID (1-26) IN AN ADJACENT CUBE
C                      II=2 SUB-DOMAIN NUMBER OF AN ADJACENT CUBE
C                      II=3 CUBE NUMBER OF AN ADJACENT CUBE IN A DOMAIN
C                      II=4 RELATIVE LEVEL OF AN ADJACENT CUBE
C                       (-1: FINE, 0:SAME, 1:COARSE)
C                      II=5 POSITION IN COARSER CUBE
C     VISCM          : NORMALIZED VISCOCITY BY DSCALE AND VSCALE
C     DSCALE         :LENGTH OF MINIMUMU CUBE
C     VSCALE         :VELOCITY RATIO BETWEEN CHARACTEERICTIC AND PARTICLE VELOCITY 
C     MDOM           :MAX. NUMBER OF THE NEIGHBORING SUB-DOMAINS
C     MPB             NUMBER OF DATA TO BE RECEIVED FROM THE NEIGHBORING 
C                     SUB-DOMAINS
C    NDOM            :NUMBER OF THE NEIGHBORING SUB-DOMAINS
C    LDOM(IDOM)      :NEIGHBORING SUB-DOMAIN NUMBER
C    NBPRCV(IDOM)    :NUMBER OF DATA TO BE RECEIVED FROM THE NEIGHBORING SUB-DOMAINS
C    NBPSND(IDOM)    :NUMBER OF DATA TO SEND TO THE NEIGHBORING SUB-DOMAINS
C 
C[IN-OUT]
C     F(I,J,K,IP,IC) :PARTICLE DISTRIBUTION FUNCTION
C
C 
C[WORK]
C     F(I,J,K,IP,IC)   :PARTICLE EQUILIBRIUM DISTRIBUTION FUNCTION
C     WRKRCV(IPB,IDOM) :WORK REGION FOR RECEIVING THE PARTICLE DISTRIBUTION FUNCTION
C     WRKSND(IPB,IDOM) :WORK REGION FOR RECEIVING THE PARTICLE DISTRIBUTION FUNCTION
C     FBC1 (IP,I,J,IB,IC) :WORK REGION FOR FACE-TYPE  B.C. GROUPS
C     FBC2 (IP,I,  IB,IC) :WORK REGION FOR EDGE-TYPE  B.C. GROUPS
C     FBC3 (IP,    IB,IC) :WORK REGION FOR POINT-TYPE B.C. GROUPS
C     FBC1W(IP,I,J,IB,IC) :WORK REGION FOR FACE-TYPE  B.C. GROUPS
C     FBC2W(IP,I,J IB,IC) :WORK REGION FOR EDGE-TYPE  B.C. GROUPS
C     FBC3W(IP,    IB,IC) :WORK REGION FOR POINT-TYPE B.C. GROUPS
C
      CALL USTSTA(11)
C
C[1] SET CUBE-B.C. DATA TO WORK REIGION FBC[1,2,3]
C
      CALL USTSTA(12)
      DO 1000 IC=1,NC
          ILVL2=LLEVEL(IC)
          ILVL1=MIN(1,ILVL2-1)
          ILVL3=      ILVL2+1
          VIS1= VISCM*(VSCALE/DSCALE)*DBLE(NG)/(2.0E0**ILVL1)
          VIS2= VISCM*(VSCALE/DSCALE)*DBLE(NG)/(2.0E0**ILVL2)
          VIS3= VISCM*(VSCALE/DSCALE)*DBLE(NG)/(2.0E0**ILVL3)
          TAU3(1)= 0.5D0+3.0D0*VIS1
          TAU3(2)= 0.5D0+3.0D0*VIS2
          TAU3(3)= 0.5D0+3.0D0*VIS3
C
C
C NOTE THAT EQUILIBRIUM DISTRIBUTION FUNCTION IS NOT NECESSARY
C IF ALL THE NEIBERING CUBES HAVE SAME LEVEL
          DO 1001 ICB=1,NBC(IC)
              IF(LBC(4,ICB,IC).NE.0) GOTO 1002
 1001     CONTINUE
          GOTO 1003
C
 1002     CONTINUE
C
C
          DO 1101 K =0,NG+2
          DO 1102 J =0,NG+2
          DO 1103 I =0,NG+2
              RHO=0.0D0
              U0 =0.0D0
              V0 =0.0D0
              W0 =0.0D0
              DO 1104 IP=1,NP
                  FBUF=F(I,J,K,IP,IC)
                  RHO=RHO+FBUF
                  U0 =U0 +FBUF*CVEL(1,IP)
                  V0 =V0 +FBUF*CVEL(2,IP)
                  W0 =W0 +FBUF*CVEL(3,IP)
 1104         CONTINUE
              U0=U0/RHO 
              V0=V0/RHO 
              W0=W0/RHO 
              UU =U0*U0+V0*V0+W0*W0
              DO 1105 IP=1,NP
                  CU= U0*CVEL(1,IP)+V0*CVEL(2,IP)+W0*CVEL(3,IP)
                  FBUF=WF(IP)*RHO*(1.0D0+3.0D0*CU+4.5D0*CU*CU-1.5D0*UU)
                  FEQ(I,J,K,IP)= FBUF
 1105         CONTINUE
 1103     CONTINUE
 1102     CONTINUE
 1101     CONTINUE
C
 1003     CONTINUE
          NBC1=0
          NBC2=0
          NBC3=0
          DO 1200 ICB=1,NBC(IC)
              CALL SETBC1(ITIME,NG,NP,MBC,MBC1,MBC2,MBC3,
     *                    LLEVEL(IC),LBC(1,ICB,IC),TAU3,LVEL,
     *                    F(0,0,0,1,IC),FEQ,NBC1,NBC2,NBC3,
     *                    FBC1(1,1,1,1,IC),FBC1W(1,1,1,1,IC),
     *                    FBC2(1,1,1,  IC),FBC2W(1,1,1  ,IC),
     *                    FBC3(1,1,    IC),FBC3W(1,1    ,IC),
     *                    IUT6)
 1200     CONTINUE
 1000 CONTINUE
      CALL USTEND(12)
C
C[2] SET CUBE-B.C. DATA FROM THE SELF-CUBE 
C
C
C
      CALL USTSTA(13)
      CALL SETBC2(IPART,NG,NP,NC,MBC,MBC1,MBC2,MBC3,NBC,LBC,
     *            MDOM,MPB,NDOM,LDOM,F,FBC1,FBC2,FBC3,BUFSND,
     *            NBPSND,NBPRCV,IUT6)
      CALL USTEND(13)
C
C[3] COMMUNICATE CUBE-B.C. IN THE NEIBERING-CUBE 
C
      CALL USTSTA(14)
      CALL DDCOM2(NDOM,LDOM,MPB,NBPSND,NBPRCV,BUFSND,BUFRCV,IERR)
      CALL USTEND(14)
C
C[4] SET CUBE-B.C. FROM THE NEIBERING-CUBE 
C
      CALL USTSTA(15)
      DO 2000 IDOM=1,NDOM
          CALL SETBC3(NG,NP,NC,MPB,F,BUFRCV(1,IDOM),IUT6)
 2000 CONTINUE
      CALL USTEND(15)
C
      CALL USTEND(11)
C
      RETURN
      END
