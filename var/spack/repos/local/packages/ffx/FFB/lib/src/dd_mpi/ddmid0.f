      SUBROUTINE DDMID0(LPMID1,LPMID2,LPMID3,NPMID,MPT,MDOM,IUT0,
     *                  NGAT,LGAT,NPTGAT,IPMID,IPGAT,
     *                  NSCT,LSCT,NPTSCT,IEMID,IPSCT,IERR)
      IMPLICIT NONE
      INTEGER*4 NPMID,MPT,MDOM,NGAT,NSCT,IUT0,IERR
      INTEGER*4 LPMID1(NPMID),LPMID2(NPMID),LPMID3(NPMID),
     *          LGAT(MDOM),NPTGAT(MDOM),IPMID(MPT,MDOM),IPGAT(MPT,MDOM),
     *          LSCT(MDOM),NPTSCT(MDOM),IEMID(MPT,MDOM),IPSCT(MPT,MDOM)
C
      INTEGER*4 IDOM,NDUM,IBP,IFNEW,ICHK
C
      CHARACTER*60 ERMSGB
     & / ' ## SUBROUTINE DDMID0: FATAL     ERROR OCCURRENCE; RETURNED' /
      CHARACTER*60 EREXP1
     & / ' DIMENSION SIZE OF PASSED LIST ARRAYS IS NOT SUFFICIENT    ' /
C
C
C      SET UP MID NODE SUB-DOMAIN LISTS FOR DOMAIN-DECOMPOSITION
C     PROGRAMMING MODEL
C 
C     WRITTEN BY Y.YAMADE 2010.12.29
C
C
C     ARGUMENT LISTINGS
C       (1) INPUT
C INT *4   LPMID1  (IB); MID INODES
C INT *4   LPMID2  (IB); PARANTS ELEMENT2 OF MID NODES
C INT *4   LPMID3  (IB); DOMAIN NUMBER TO SEND/RECEIVE 
C          0 --- THE DOMAIN HAS BOTH OF MID NODE AND ITS PARANT ELEMENT 
C (POS. INT.)--- THE DOMAIN HAS ONLY PARANT ELEMENT 
C (NEG. INT.)--- THE DOMAIN HAS ONLY MID NODE 
C          NPMID       ; NUMBER OF MID NODES
C INT *4   MPT         ; THE DIMENSION SIZE OF THE FIRST ELEMENTS OF THE 
C                        PASSED ARRAYS 'IPSET', 'IPGAT', 'IEMID', AND 'IPSCT'
C                        (I.E. THE MAXIMUM NUMBER OF THE MID NODES FOR A 
C                         SINGLE SUB-DOMAIN)
C INT *4   MDOM        ; MAX. NUMBER OF THE OVERSET SUB-DOMAINS
C INT *4   IUT0        ; FILE NUMBER TO WRITE ERROR MESSAGE
C
C       (2) OUTPUT
C INT *4   NGAT             ; NUMBER OF DOMAINS FOR GATHERING
C INT *4   LGAT      (IDOM) ; DOMAIN NUMBER     FOR GATHERING
C INT *4   NPTGAT    (IDOM) ; NUMBER OF GATHERED VALUES FOR THE DOMAIN 'LGAT'
C INT *4   IPMID (IPT,IDOM) ; MIN NODE NUMBER IN THE DOMAIN 
C                             WHICH WILL RECEIVE GATHERED VALUES 
C INT *4   IPGAT (IPT,IDOM) ; INDICATES POSITION OF GATHERED DATA
C                             IN THE PASSING MID NODE ARRAYS
C
C INT *4   NSCT             ; NUMBER OF DOMAINS FOR SCATTERING
C INT *4   LSCT      (IDOM) ; DOMAIN NUMBER     FOR SCATTERING
C INT *4   NPTSCT    (IDOM) ; NUMBER OF VALUES TO BE SCATTERED 
C                             FOR THE DOMAIN 'LGAT'
C INT *4   IEMID (IPT,IDOM) ; PARANT ELEMENT NUMBER IN THE DOMAIN
C                             WHICH WILL RECEIVE VALUES AND SCATTER IT 
C INT *4   IPSCT (IPT,IDOM) ; INDICATES POSITION OF DATA TO BE SENT AND 
C                             SCATTERED IN THE PASSING MID NODE ARRAYS
C
C INT *4   IERR             ; RETURN CODE WHOSE VALUE WILL BE EITHER
C                   0 --- INDICATING SUCCESSFUL TERMINATION
C                OR 1 --- INDICATING OCCURRENCE OF SOME ERROR CONDITIONS
C
C
      IERR = 0
C
C
C
C SET INITIAL VALUES
C
C
C
      NGAT = 0
      NSCT = 0
      DO 1000 IDOM = 1 , MDOM
          NPTGAT(IDOM) = 0
          NPTSCT(IDOM) = 0
 1000 CONTINUE
C
C
C
C SET DOMAIN LISTS FOR GATHERING
C
C
C
      NDUM = 0
      DO 2000 IBP = 1 , NPMID
          IF(LPMID3(IBP).LE.0) GO TO 2000
C
          NDUM = NDUM+1
C
          IFNEW = LPMID3(IBP)
          DO 2100 ICHK = 1 , NGAT
              IF(LGAT(ICHK).EQ.IFNEW) THEN
                  IDOM = ICHK
                  GO TO 2200
              ENDIF
 2100     CONTINUE
          NGAT = NGAT+1
          IDOM = NGAT
C
          IF(NGAT.GT.MDOM) THEN
              WRITE(IUT0,*) ERMSGB
              WRITE(IUT0,*) EREXP1
              IERR = 1
              RETURN
          ENDIF
C
          LGAT(NGAT) = IFNEW
C
 2200     CONTINUE
          NPTGAT(IDOM) = NPTGAT(IDOM)+1
C
          IF(NPTGAT(IDOM).GT.MPT) THEN
              WRITE(IUT0,*) ERMSGB
              WRITE(IUT0,*) EREXP1
              IERR = 1
              RETURN
          ENDIF
C
          IPMID(NPTGAT(IDOM),IDOM) = LPMID1(IBP)
          IPGAT(NPTGAT(IDOM),IDOM) = NDUM
 2000 CONTINUE
C
C
C
C SET DOMAIN LISTS FOR SCATTERING
C
C
C
      NDUM = 0
      DO 3000 IBP = 1 , NPMID
          IF(LPMID3(IBP).GE.0) GO TO 3000
C
          NDUM = NDUM+1
C
          IFNEW = -LPMID3(IBP)
          DO 3100 ICHK = 1 , NSCT
              IF(LSCT(ICHK).EQ.IFNEW) THEN
                  IDOM = ICHK
                  GO TO 3200
              ENDIF
 3100     CONTINUE
          NSCT = NSCT+1
          IDOM = NSCT
C
          IF(NSCT.GT.MDOM) THEN
              WRITE(IUT0,*) ERMSGB
              WRITE(IUT0,*) EREXP1
              IERR = 1
              RETURN
          ENDIF
C
          LSCT(NSCT) = IFNEW
C
 3200     CONTINUE
          NPTSCT(IDOM) = NPTSCT(IDOM)+1
C
          IF(NPTSCT(IDOM).GT.MPT) THEN
              WRITE(IUT0,*) ERMSGB
              WRITE(IUT0,*) EREXP1
              IERR = 1
              RETURN
          ENDIF
C
          IEMID(NPTSCT(IDOM),IDOM) = LPMID2(IBP)
          IPSCT(NPTSCT(IDOM),IDOM) = NDUM
 3000 CONTINUE
C
      RETURN
      END
C
