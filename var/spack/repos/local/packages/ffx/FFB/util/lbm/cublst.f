      PROGRAM CUBLST
      IMPLICIT NONE
C
C     VER:2021.04.23
C
      INTEGER*4 MBC
      PARAMETER (MBC=152)
C
CCCC  FOR CUBES 
      INTEGER*4, ALLOCATABLE:: NCPT(:)
      INTEGER*4, ALLOCATABLE:: LLEVEL(:),LPOSI(:,:)
      INTEGER*4, ALLOCATABLE:: NBC(:),LBC(:,:,:)
      INTEGER*4, ALLOCATABLE:: LPART(:),LCUBE(:),NDOM(:),LDOM(:,:)
      INTEGER*4, ALLOCATABLE:: LISTCB(:,:)
C
C    LBC(II,IBC,IC)   ATTRIBUTE DATA OF B.C. GROUPS
C                     II=1 B.C. GROUP ID (1-26) IN AN ADJACENT CUBE
C                     II=2 SUB-DOMAIN NUMBER OF AN ADJACENT CUBE
C                     II=3 CUBE NUMBER OF AN ADJACENT CUBE IN A DOMAIN
C                     II=4 RELATIVE LEVEL OF AN ADJACENT CUBE
C                      (-1: FINE, 0:SAME, 1:COARSE)
C                     II=5 POSITION IN COARSER CUBE
C
C
      INTEGER*4 NG,MC,NPART,MDOM,LERR(16)
C
      INTEGER*4 IUT0,IUT5,IUT6,IUTCB,IUTOT,IERR,IMODE,JMODE
      DATA IUT0  / 0/
      DATA IUT5  / 5/
      DATA IUT6  / 6/
      DATA IUTCB /11/
      DATA IUTOT /12/
      DATA IERR  / 0/
      DATA IMODE / 2/
      DATA JMODE / 2/
C
      CHARACTER*60 FILECB,FILEOT,FILE
      INTEGER*4 MCOM,NCOMFL,NCOMST
      PARAMETER(MCOM=10)
      CHARACTER*60 COMFLE(MCOM),COMSET(MCOM)
      DATA NCOMFL /0/
      DATA NCOMST /0/
C
      INTEGER*4 NC,NCG,IC,JC,IPART,I,J,IMATCH
      REAL*4    DSCALE,DD,BBOX1(6),BBOX2(6)
C
C
      WRITE(IUT6,*) 'SPECIFY NAME OF GF-CUBE FILE'
      READ (IUT5,'(A60)') FILECB
C
      WRITE(IUT6,*) 'SPECIFY NAME OF CUBE-LIST FILE'
      READ (IUT5,'(A60)') FILEOT
C
      WRITE(IUT6,*) 'SPECIFY NUMBER OF REGIONS'
      READ (IUT5,*) NPART
C
      WRITE(IUT6,*) 'SPECIFY DSCALE'
      READ (IUT5,*) DSCALE
C
      WRITE(IUT6,*) 'SPECIFY BOUNDING BOX'
      READ (IUT5,*) (BBOX2(I),I=1,6)
C
      WRITE(IUT6,*) 
      WRITE(IUT6,'(A24,A24)'  ) 'NAME OF GF-CUBE FILE  : ',FILECB
      WRITE(IUT6,'(A24,I8)'   ) 'NPART                 : ',NPART
      WRITE(IUT6,'(A24,F10.5)') 'DSCALE                : ',DSCALE
      WRITE(IUT6,'(A24,F10.5)') 'BOUNDING BOX X-MIN    : ',BBOX2(1)
      WRITE(IUT6,'(A24,F10.5)') 'BOUNDING BOX Y-MIN    : ',BBOX2(2)
      WRITE(IUT6,'(A24,F10.5)') 'BOUNDING BOX Z-MIN    : ',BBOX2(3)
      WRITE(IUT6,'(A24,F10.5)') 'BOUNDING BOX X-MAX    : ',BBOX2(4)
      WRITE(IUT6,'(A24,F10.5)') 'BOUNDING BOX Y-MAX    : ',BBOX2(5)
      WRITE(IUT6,'(A24,F10.5)') 'BOUNDING BOX Z-MAX    : ',BBOX2(6)
C
      MC=1
      ALLOCATE(LLEVEL(     MC),STAT=LERR(01))
      ALLOCATE(LPOSI (   3,MC),STAT=LERR(02))
      ALLOCATE(NBC  (      MC),STAT=LERR(03))
      ALLOCATE(LBC  (5,MBC,MC),STAT=LERR(04))
      ALLOCATE(NCPT (   NPART),STAT=LERR(05))
      IMODE=0
      DO 1000 IPART=1,NPART
          CALL MFNAME(FILECB,FILE,IPART,IUT0,IERR)
          CALL GFCUBE(IMODE,MC,MBC,FILE,
     *                NG,NC,LLEVEL,LPOSI,NBC,LBC,
     *                MCOM,NCOMFL,NCOMST,COMFLE,COMSET,
     *                IUT6,IUT0,IUTCB,IERR)
          NCPT(IPART)=NC
      IF(NC.GE.MC) MC=NC
 1000 CONTINUE
      DEALLOCATE(LLEVEL)
      DEALLOCATE(LPOSI)
      DEALLOCATE(NBC)
      DEALLOCATE(LBC)
C
      NCG=MC*NPART
      ALLOCATE(LLEVEL(      MC),STAT=LERR(01))
      ALLOCATE(LPOSI (    3,MC),STAT=LERR(02))
      ALLOCATE(NBC   (      MC),STAT=LERR(03))
      ALLOCATE(LBC   (5,MBC,MC),STAT=LERR(04))
      ALLOCATE(LISTCB(   2,NCG),STAT=LERR(05))
C
      NCG=0
      IMODE=1
      DO 2000 IPART=1,NPART
          NC=NCPT(IPART)  
          CALL MFNAME(FILECB,FILE,IPART,IUT0,IERR)
          CALL GFCUBE(IMODE,MC,MBC,FILE,
     *                NG,NC,LLEVEL,LPOSI,NBC,LBC,
     *                MCOM,NCOMFL,NCOMST,COMFLE,COMSET,
     *                IUT6,IUT0,IUTCB,IERR)
C
          DO 2100 IC=1,NC
              DD=DSCALE*2.0E0**(LLEVEL(IC)-1)
              BBOX1(1)=DSCALE*LPOSI(1,IC)          
              BBOX1(2)=DSCALE*LPOSI(2,IC)          
              BBOX1(3)=DSCALE*LPOSI(3,IC)          
              BBOX1(4)=BBOX1(1)+DD
              BBOX1(5)=BBOX1(2)+DD
              BBOX1(6)=BBOX1(3)+DD
C
              CALL CHKBB(IMATCH,BBOX1,BBOX2)
              IF(IMATCH.EQ.1) THEN
                  NCG=NCG+1
                  LISTCB(1,NCG)=IPART
                  LISTCB(2,NCG)=IC
              ENDIF
C
C          WRITE(90,'(7I8,12F8.3,I8)')
C     *    IMATCH,IPART,IC,LLEVEL(IC),
C     *    (LPOSI(I,IC),I=1,3),
C     *    (BBOX1(J),J=1,6),
C     *    (BBOX2(J),J=1,6)
 2100 CONTINUE
C
 2000 CONTINUE
C
      OPEN(IUTOT,FILE=FILEOT)
      WRITE(IUTOT,'(2I8)') NCG 
      DO 3000 I=1,NCG
          WRITE(IUTOT,'(2I8)') LISTCB(1,I),LISTCB(2,I) 
 3000 CONTINUE
      CLOSE(IUTOT)
C
      STOP
      END
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE CHKBB(IMATCH,BB1,BB2)
      IMPLICIT NONE
      INTEGER*4 IMATCH
      REAL*4    BB1(6),BB2(6)
      REAL*4    X1MIN,X1MAX,Y1MIN,Y1MAX,Z1MIN,Z1MAX
      REAL*4    X2MIN,X2MAX,Y2MIN,Y2MAX,Z2MIN,Z2MAX
      INTEGER*4 IX,IY,IZ 
C
      X1MIN=BB1(1)     
      Y1MIN=BB1(2)     
      Z1MIN=BB1(3)     
      X1MAX=BB1(4)     
      Y1MAX=BB1(5)     
      Z1MAX=BB1(6)     
C
      X2MIN=BB2(1)     
      Y2MIN=BB2(2)     
      Z2MIN=BB2(3)     
      X2MAX=BB2(4)     
      Y2MAX=BB2(5)     
      Z2MAX=BB2(6)     
C
      IX=0
      IY=0
      IZ=0
C
      IF(X2MIN.LE.X1MIN .AND. X1MIN.LE.X2MAX) IX=1
      IF(X2MIN.LE.X1MAX .AND. X1MAX.LE.X2MAX) IX=1
      IF(Y2MIN.LE.Y1MIN .AND. Y1MIN.LE.Y2MAX) IY=1
      IF(Y2MIN.LE.Y1MAX .AND. Y1MAX.LE.Y2MAX) IY=1
      IF(Z2MIN.LE.Z1MIN .AND. Z1MIN.LE.Z2MAX) IZ=1
      IF(Z2MIN.LE.Z1MAX .AND. Z1MAX.LE.Z2MAX) IZ=1
C
      IMATCH=IX*IY*IZ
C
      RETURN
      END

C
