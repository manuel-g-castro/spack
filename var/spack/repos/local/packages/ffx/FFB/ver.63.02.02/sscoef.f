      SUBROUTINE SSCOEF(IMODE,N2,NEX,NE,NP,NODE,X,Y,Z,NUMSSB,CODSSB,
     *                  MZ,MBLK,NDIVX,NDIVY,NDIVZ,NEZ,IELIST,ISTART,
     *                  XMIN,YMIN,ZMIN,XMAX,YMAX,ZMAX,
     *                  XMINE,YMINE,ZMINE,XMAXE,YMAXE,ZMAXE,
     *                  NPASRC,LPASRC,LEASRC,COEAS1,COEAS2,COEAS3,
     *                  IUT0,IERR)
      IMPLICIT NONE
C
C[INPUT]
      INTEGER*4 IMODE,N2,NEX(8),NE,NP,NODE(N2,NE),NUMSSB(3),IUT0
      INTEGER*4 MZ,MBLK,NDIVX,NDIVY,NDIVZ,
     *          NEZ(MZ),IELIST(MBLK),ISTART(MZ)
      REAL*4    CODSSB(4),X(NP),Y(NP),Z(NP),
     *          XMIN,YMIN,ZMIN,XMAX,YMAX,ZMAX, 
     *          XMINE(NE),YMINE(NE),ZMINE(NE),
     *          XMAXE(NE),YMAXE(NE),ZMAXE(NE)
C
C[OUTPUT]
      INTEGER*4 NPASRC,LPASRC(*),LEASRC(*),IERR
      REAL*4    COEAS1(*),COEAS2(*),COEAS3(*)
C
C[LOCAL]
      INTEGER*4 IX,IY,IZ,NX,NY,NZ,IGP,IE
      INTEGER*4 INX,INY,INZ,IZZ
      REAL*4    XP,YP,ZP,COEF1,COEF2,COEF3,X0,Y0,Z0,DD
C
C
C     NOTE THAT IMODE CONTROLS ACTION AS FOLLOWS
C      IMODE=1  : COUNT NOUNBER OF SOUND SOURCE POINTS
C      IMODE=2  : SORE NOSED NUMBER AND COEFFICIENT
C
      IERR=0
C
      IGP=0
      NPASRC=0
C
      NX=NUMSSB(1)
      NY=NUMSSB(2)
      NZ=NUMSSB(3)
      X0=CODSSB(1)
      Y0=CODSSB(2)
      Z0=CODSSB(3)
      DD=CODSSB(4)
C
      DO 1000 IZ=0,NZ
      DO 1100 IY=0,NY
      DO 1200 IX=0,NX
C
          IGP=IGP+1
          XP=X0+DD*FLOAT(IX)
          YP=Y0+DD*FLOAT(IY)  
          ZP=Z0+DD*FLOAT(IZ)  
C
          INX = NDIVX*(XP-XMIN)/(XMAX-XMIN)+1
          INY = NDIVY*(YP-YMIN)/(YMAX-YMIN)+1
          INZ = NDIVZ*(ZP-ZMIN)/(ZMAX-ZMIN)+1
          IF(INX.LT.1) INX = 1
          IF(INY.LT.1) INY = 1
          IF(INZ.LT.1) INZ = 1
          IF(INX.GT.NDIVX) INX = NDIVX
          IF(INY.GT.NDIVY) INY = NDIVY
          IF(INZ.GT.NDIVZ) INZ = NDIVZ
          IZZ = (INZ-1)*(NDIVX*NDIVY)+(INY-1)*NDIVX+INX
          IF(NEZ(IZZ).EQ.0) GOTO 1200
C
          CALL FNDSSB(N2,NEZ(IZZ),NE,NP,NEX,X,Y,Z,
     *                NODE,IELIST(ISTART(IZZ)),
     *                XMINE,YMINE,ZMINE,
     *                XMAXE,YMAXE,ZMAXE,
     *                XP,YP,ZP,IE,COEF1,COEF2,COEF3,IUT0,IERR)
C
          IF(IE.EQ.0) GOTO 1200
C
          NPASRC=NPASRC+1
          IF(IMODE.NE.2) GOTO 1200 
C
          LPASRC(NPASRC)=IGP
          LEASRC(NPASRC)=IE
          COEAS1(NPASRC)=COEF1
          COEAS2(NPASRC)=COEF2
          COEAS3(NPASRC)=COEF3
C
 1200 CONTINUE
 1100 CONTINUE
 1000 CONTINUE
C
      RETURN
      END
