      SUBROUTINE NODLEX
     *   (NODE,ME,MP,NE,NP,N1,N2,NEX,SN,
     *    IPART,LDOM,NBPDOM,NDOM,IPSLF,IPSND,MBPDOM,
     *    VALELM,VALNOD,CM,IUT0,IERR,BUFSND,BUFRCV,MAXBUF)
C
      IMPLICIT NONE
C
      INTEGER*4 ME,MP,NE,NP,N1,N2
      INTEGER*4 NODE(N2,NE),NEX(8)
      REAL*4 SN (N1,ME)
      INTEGER*4 IPART,NDOM,MBPDOM
      INTEGER*4 LDOM(NDOM),NBPDOM(NDOM)
      INTEGER*4 IPSLF(MBPDOM,NDOM),IPSND(MBPDOM,NDOM)
      REAL*4 VALELM(ME),VALNOD(NP),CM(NP)
      INTEGER*4 IUT0,IERR,MAXBUF
      REAL*4 BUFSND(MAXBUF),BUFRCV(MAXBUF)
C
      CHARACTER*60 ERMSGC
     & /' ## SUBROUTINE NODLEX: FATAL      ERROR REPORT   ; RETURNED' /
C
      INTEGER*4 NETET,NEPRD,NEWED,NEHEX
      INTEGER*4 NTET, NPRD, NWED, NHEX
      INTEGER*4 IES1,IES2,IES3,IES4,IEE1,IEE2,IEE3,IEE4
C
      INTEGER*4 I,IE,IP,IDUM
C
C     * START *
C
      CALL USTSTA(72)
C
      NETET=NEX(1)
      NEPRD=NEX(2)
      NEWED=NEX(3)
      NEHEX=NEX(4)
      NE   =NETET+NEPRD+NEWED+NEHEX
C
      NTET =NEX(5)
      NPRD =NEX(6)
      NWED =NEX(7)
      NHEX =NEX(8)
C
C     * TET *
      IES1=1
      IEE1=NETET
C
C     * PYRAMID *
      IES2=NETET+1
      IEE2=NETET+NEPRD
C
C     * WEDGE
      IES3=NETET+NEPRD+1
      IEE3=NETET+NEPRD+NEWED
C
C     * HEX *
      IES4=NETET+NEPRD+NEWED+1
      IEE4=NETET+NEPRD+NEWED+NEHEX
C
C
C     *** ZERO ***
C
C
      DO 300 IP=1,NP
         VALNOD(IP)=0.0E0
 300  CONTINUE
C
C     
C     *** VALNOD = N * VALELM ***
C
C     * TET *
      DO 101 I=1,NTET
         DO 100 IE=IES1,IEE1
            IP = NODE(I,IE)
            VALNOD(IP) = VALNOD(IP) + SN(I,IE)*VALELM(IE)
 100     CONTINUE
 101  CONTINUE
C
C     * PYRAMID *
      DO 103 I=1,NPRD
         DO 102 IE=IES2,IEE2
            IP = NODE(I,IE)
            VALNOD(IP) = VALNOD(IP) + SN(I,IE)*VALELM(IE)
 102     CONTINUE
 103  CONTINUE
C
C     * WEDGE *
      DO 105 I=1,NWED
         DO 104 IE=IES3,IEE3
            IP = NODE(I,IE)
            VALNOD(IP) = VALNOD(IP) + SN(I,IE)*VALELM(IE)
 104     CONTINUE
 105  CONTINUE
C
C     * HEX *
      DO 107 I=1,NHEX
         DO 106 IE=IES4,IEE4
            IP = NODE(I,IE)
            VALNOD(IP) = VALNOD(IP) + SN(I,IE)*VALELM(IE)
 106     CONTINUE
 107  CONTINUE
C
C
C     *** DATA COMMUNICATION ***
C
C
      IDUM=1
      CALL DDCOMX(IPART,IDUM,LDOM,NBPDOM,NDOM,IPSLF,IPSND,MBPDOM,
     &            VALNOD,VALNOD,VALNOD,NP,IUT0,IERR,
     &            BUFSND,BUFRCV,MAXBUF)
      IF(IERR.NE.0) THEN
         WRITE(IUT0,*)
         WRITE(IUT0,*) ERMSGC
         RETURN
      END IF   
C
C
C     *** INVERSE MASS MATRIX ***
C
C
      DO 200 IP=1,NP
         VALNOD(IP)=VALNOD(IP)*CM(IP)
 200  CONTINUE
C
      CALL USTEND(72)
C
      RETURN
      END
